plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.palja'
version = System.getenv("VERSION") ?: '0.0.0'

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'common-module'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri(System.getenv("PACKAGES_URL") ?: findProperty("PACKAGES_URL"))
            credentials {
                username = System.getenv("USERNAME") ?: findProperty("USERNAME")
                password = System.getenv("TOKEN") ?: findProperty("TOKEN")
            }
        }
    }
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.5.8'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
}